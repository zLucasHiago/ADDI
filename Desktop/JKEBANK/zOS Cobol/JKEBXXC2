      *****************************************************************         
      * Licensed Materials - Property of IBM                          *         
      *                                                               *         
      * JKEBXXC2.cbl                                                  *         
      *                                                               *         
      * B) Copyright IBM Corporation 2012                              *        
      * U.S. Government Users Restricted Rights:  Use, duplication    *         
      * or disclosure                                                 *         
      * restricted by GSA ADP Schedule Contract with IBM Corp.        *         
      *                                                               *         
                                                                                
      * PROCESS CICS('COBOL3,APOST,SP')                                         
      * PROCESS APOST TRUNC(BIN) NOSSRANGE SIZE(4000K) TEST                     
      *****************************************************************         
      *                                                               *         
      *  DFH0CALL DIVIDED INTO CLIENT AND SERVER FOR BANKING DEMO     *         
      *****************************************************************         
       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. JKEBXXC2.                                                    
       ENVIRONMENT DIVISION.                                                    
       DATA DIVISION.                                                           
       WORKING-STORAGE SECTION.                                                 
       77  MESSAGES     PIC X(39).                                              
       01  TEMP-NAME    PIC X(20).                                              
       01  FILLER REDEFINES TEMP-NAME.                                          
           02  TEMPA    PIC X OCCURS 20 INDEXED BY TEMPI.                       
       77  RESPONSE     PIC 9(9)  COMP.                                         
       01  INSPECT-IND  PIC X.                                                  
           88  NO-MORE  VALUE '1'.                                              
                        COPY JKMXXGA.                                           
                        COPY JKMXXGB.                                           
       01 CONSTANTS.                                                            
          05 CRUD-PGM-NAME PIC X(8) VALUE 'JKEBXXS1'.                           
          05 MENU-MAPS     PIC X(8) VALUE 'JKMXXGA '.                           
          05 DETAIL-MAPS   PIC X(8) VALUE 'JKMXXGB '.                           
          05 MENU-TRAN     PIC X(4) VALUE 'JKEM'.                               
      *    Comment out the next line and remove the one of the comment          
      *    from the next two lines to pick a right currency symobol             
          05 INIT-AMOUNT   PIC X(8) VALUE '$0000.00'.                           
      *    05 INIT-AMOUNT   PIC X(8) VALUE 'B#0000.00'.                         
      *    05 INIT-AMOUNT   PIC X(8) VALUE 'B%0000.00'.                         
      *                                                                         
      *    NEXT FIELD TO VERIFY AMOUNTI                                         
      *                                                                         
       01  AMOUNTN.                                                             
           03  AMOUNTN1  PIC X.                                                 
           03  AMOUNTN25 PIC X(4).                                              
           03  AMOUNTN6  PIC X.                                                 
           03  AMOUNTN78 PIC X(2).                                              
       01  LOGA.        COPY JKEBLOG.                                           
                        COPY DFHBMSCA.                                          
       01  LINK-AREA.                                                           
           05  LINKA.    COPY JKEBLINK.                                         
           05  FILEA.    COPY JKEBACCT.                                         
           05  COMM-AREA.    COPY JKEBACCT.                                     
       LINKAGE SECTION.                                                         
       01  DFHCOMMAREA.                                                         
           05  FILLER.    COPY JKEBLINK.                                        
           05  FILLER.    COPY JKEBACCT.                                        
           05  FILLER.    COPY JKEBACCT.                                        
       PROCEDURE DIVISION.                                                      
      *                                                                         
      *    THE "ERROR" EXIT IS SET UP. THIS IS TO TRAP ANY UNEXPECTED           
      *    ERRORS, ALL OTHER ERROR CONDITIONS ARE TESTED FOR EXPLICITLY         
      *    BY THE PROGRAM, USING THE "RESP" OPTION ON COMMANDS.                 
      *                                                                         
           EXEC CICS HANDLE CONDITION ERROR(ERRORS) END-EXEC.                   
      *                                                                         
           MOVE DFHCOMMAREA TO LINK-AREA.                                       
      *                                                                         
      *    THE LENGTH OF THE "LINK AREA" IS TESTED. IF >= LINK-AREA THEN        
      *    THIS IS THE VALIDATION STAGE OF AN ADD OR UPDATE.                    
      *                                                                         
           IF EIBCALEN >= LENGTH OF LINK-AREA THEN                              
              GO TO READ-INPUT.                                                 
                                                                                
           MOVE LOW-VALUES TO DETAILO.                                          
      *                                                                         
           IF ACCOUNT-OPEN OF LINK-AREA THEN                                    
              MOVE 'OPEN NEW ACCOUNT' TO TITLEO                                 
              MOVE 'ENTER DATA AND PRESS ENTER KEY' TO MSG3O                    
      *                                                                         
              MOVE KEYNUM OF LINK-AREA TO NUMBO                                 
      *                                                                         
      *       FOR THE "ADDS" TRANSACTION, THE AMOUNT FIELD HAS THE              
                                                                                
      *       ATTRIBUTE BYTE SET TO NUMERIC SO ONLY NUMERIC DATA CAN            
      *       BE ENTERED.                                                       
      *                                                                         
              MOVE DFHBMUNN TO AMOUNTA                                          
              MOVE INIT-AMOUNT TO AMOUNTO                                       
              GO TO MAP-SEND.                                                   
      *                                                                         
      *    READ THE FILE RECORD INTO THE FILE AREA.                             
      *                                                                         
           SET ACCOUNT-READ OF LINK-AREA TO TRUE.                               
           EXEC CICS LINK PROGRAM(CRUD-PGM-NAME)                                
                 RESP(RESPONSE)COMMAREA(LINK-AREA) END-EXEC.                    
      *                                                                         
      *    CHECK RESPONSE TO COMMAND                                            
      *                                                                         
           IF RESPONSE NOT = DFHRESP(NORMAL)                                    
              THEN GO TO CHECK-RESP                                             
              ELSE PERFORM CHECK-LINK-RESP.                                     
           IF ACCOUNT-VIEW OF LINK-AREA THEN                                    
      *                                                                         
      *       IF PROGRAM IS INVOKED BY " INQY", A TITLE AND COMMAND             
      *       MESSAGE ARE MOVED TO THE MAP AREA.                                
      *                                                                         
              MOVE 'ACCOUNT INQUIRY' TO TITLEO                                  
              MOVE 'PRESS ENTER TO CONTINUE' TO MSG3O                           
      *                                                                         
      *       ALL FIELD ATTRIBUTES ARE PROTECTED.                               
      *                                                                         
              MOVE DFHBMPRO TO NAMEA                                            
              MOVE DFHBMPRO TO ADDRA                                            
              MOVE DFHBMPRO TO PHONEA                                           
              MOVE DFHBMPRO TO DATEA                                            
              MOVE DFHBMPRO TO AMOUNTA                                          
              MOVE DFHBMPRO TO COMMENTA                                         
      *                                                                         
      *       THE FILE RECORD FIELDS ARE MOVED TO THE MAP AREA, AND             
      *       THE INQUIRY SCREEN IS DISPLAYED.                                  
      *                                                                         
              PERFORM MAP-BUILD THRU MAP-SEND                                   
      *                                                                         
      *       THIS INVOCATION OF THE PROGRAM TERMINATES.                        
      *       THE "MENU" TRANSACTION WILL TO BE INVOKED                         
      *       WHEN THE NEXT RESPONSE IS RECEIVED FROM THE TERMINAL.             
      *                                                                         
              EXEC CICS RETURN TRANSID(MENU-TRAN) END-EXEC.                     
      *                                                                         
           IF ACCOUNT-CHANGE OF LINK-AREA THEN                                  
              MOVE 'ACCOUNT UPDATE' TO TITLEO                                   
              MOVE 'CHANGE FIELDS AND PRESS ENTER' TO MSG3O .                   
                                                                                
       MAP-BUILD.                                                               
      *                                                                         
      *    THE FIELDS FROM THE FILE AREA ARE MOVED TO THE MAP AREA              
      *                                                                         
           MOVE NUMB    IN FILEA TO NUMBO.                                      
           MOVE NAME    IN FILEA TO NAMEO.                                      
           MOVE ADDRX   IN FILEA TO ADDRO.                                      
           MOVE PHONE   IN FILEA TO PHONEO.                                     
           MOVE DATEX   IN FILEA TO DATEO.                                      
           MOVE AMOUNT  IN FILEA TO AMOUNTO.                                    
           MOVE COMMENT IN FILEA TO COMMENTO.                                   
                                                                                
       MAP-SEND.                                                                
      *                                                                         
      *    THE SCREEN IS ERASED BEFORE THE MAP IS DISPLAYED.                    
      *                                                                         
           EXEC CICS SEND MAP('DETAIL') MAPSET(DETAIL-MAPS)                     
                     ERASE END-EXEC.                                            
       FIN.                                                                     
      *                                                                         
      *    THIS PROGRAM IS INVOKED WITH NEXT TERMINAL RESPONSE.                 
      *                                                                         
           EXEC CICS RETURN TRANSID(EIBTRNID)                                   
                 COMMAREA(LINK-AREA)  END-EXEC.                                 
                                                                                
      *                                                                         
      *    CONTROL IS PASSED HERE WHEN THE TEST OF "EIBCALEN", AT THE           
      *    BEGINING  OF THE PROGRAM, FINDS THAT A "LINK-AREA" HAS BEEN          
      *    RECEIVED. THIS PART OF THE PROGRAM MAPS IN DATA FOR AN ADD           
      *    OR UPDATE REQUEST, PERFORMS VALIDATION AND UPDATES "FILEA".          
      *                                                                         
       READ-INPUT.                                                              
      *                                                                         
      *    THE "RECEIVE MAP" COMMAND MAPS IN THE VARIABLES FROM THE             
      *    SCREEN.                                                              
      *                                                                         
           EXEC CICS RECEIVE MAP('DETAIL') MAPSET(DETAIL-MAPS)                  
                     RESP(RESPONSE) END-EXEC.                                   
           IF RESPONSE NOT = DFHRESP(NORMAL) THEN GO TO CHECK-RESP.             
                                                                                
      *                                                                         
      *          THE UPDATE FLAG IS SET IN THE RECORD AREA AND THE              
      *          MESSAGE "RECORD UPDATED" IS MOVED TO THE MESSAGE AREA          
      *          READY FOR DISPLAY ON THE OPERATOR INSTRUCTION SCREEN.          
      *                                                                         
           IF ACCOUNT-CHANGE OF LINK-AREA THEN                                  
                 MOVE 'U' TO STAT IN FILEA                                      
                 PERFORM CHECK THRU FILE-WRITE                                  
                 MOVE 'RECORD UPDATED' TO MESSAGES GO TO MENU.                  
      *                                                                         
      *    IF THIS IS AN ADD REQUEST THE ADD FLAG IS SET IN THE NEW             
      *    RECORD AND THE MESSAGE "RECORD ADDED" IS MOVED TO THE MESSAGE        
      *    AREA READY FOR DISPLAY ON THE OPERATOR INSTRUCTION SCREEN.           
      *                                                                         
           IF ACCOUNT-OPEN OF LINK-AREA THEN                                    
                 MOVE LOW-VALUES TO FILEREC IN FILEA                            
                 MOVE 'A' TO STAT IN FILEA                                      
                 PERFORM CHECK THRU FILE-WRITE                                  
                 MOVE 'RECORD ADDED' TO MESSAGES GO TO MENU.                    
      *                                                                         
      *    CHECK FIELDS ADDED/UPDATED                                           
      *                                                                         
       CHECK.                                                                   
           IF NAMEI    = LOW-VALUES AND                                         
              ADDRI    = LOW-VALUES AND                                         
      *                                                                         
      *       ANY REQUIRED EDITING STEPS SHOULD BE INSERTED HERE.               
      *       A SUITABLE FORM OF EDITING SHOULD BE USED TO ENSURE VALID         
      *       RECORDS ARE PLACED ON THE FILE. THE ROUTINE "INSP-NAME"           
      *       IS CALLED AS AN EXAMPLE, TO REMOVE ALL NON-ALPHANUMERIC           
      *       CHARACTERS AND REPLACE THEM WITH SPACES.                          
      *                                                                         
              PHONEI   = LOW-VALUES AND                                         
              DATEI    = LOW-VALUES AND                                         
              AMOUNTI  = LOW-VALUES AND                                         
              COMMENTI = LOW-VALUES                                             
              THEN MOVE 'RECORD NOT MODIFIED' TO MESSAGES                       
                   GO TO MENU.                                                  
      *                                                                         
      *   INSP-NAME CHANGES ALL NON-ALPHABETIC CHARACTERS THAT ARE              
      *   VALID IN A NAME TO SPACES SO THAT AN ALPHABETIC TEST MAY              
      *   MAY BE APPLIED TO IT. THE CHANGED NAME IS RETURNED IN                 
      *   TEMP-NAME.                                                            
      *                                                                         
           PERFORM INSP-NAME.                                                   
           IF ACCOUNT-OPEN OF LINK-AREA THEN                                    
              IF TEMP-NAME NOT ALPHABETIC THEN GO TO DATA-ERROR.                
           IF ACCOUNT-CHANGE OF LINK-AREA THEN                                  
               IF NAMEI NOT = LOW-VALUES                                        
               AND TEMP-NAME NOT ALPHABETIC THEN GO TO DATA-ERROR.              
      *                                                                         
      *    AMOUNTI MUST BE IN FORMAT B%NNNN.NN OR B#NNNN.NN OR $NNNN.NN         
      *                                                                         
           IF AMOUNTI = LOW-VALUE THEN GO TO FILE-WRITE.                        
           MOVE AMOUNTI TO AMOUNTN.                                             
           IF (AMOUNTN1 = 'B%' OR '$' OR 'B#') AND                              
              (AMOUNTN25 IS NUMERIC)  AND                                       
              (AMOUNTN6 = '.')        AND                                       
              (AMOUNTN78 IS NUMERIC)                                            
               THEN GO TO FILE-WRITE                                            
           ELSE                                                                 
               GO TO DATA-ERROR.                                                
                                                                                
       INSP-NAME.                                                               
           MOVE NAMEI TO TEMP-NAME.                                             
           MOVE SPACE TO INSPECT-IND.                                           
      *    CANNOT USE 'INSPECT' FEATURE IN OLD COBOL                            
           PERFORM SEARCH-NAME UNTIL NO-MORE.                                   
                                                                                
       SEARCH-NAME.                                                             
           SEARCH TEMPA VARYING TEMPI                                           
           AT END MOVE '1' TO INSPECT-IND                                       
           WHEN TEMPA(TEMPI) EQUAL TO '.' MOVE SPACE TO TEMPA(TEMPI)            
           WHEN TEMPA(TEMPI) EQUAL TO '-' MOVE SPACE TO TEMPA(TEMPI)            
           WHEN TEMPA(TEMPI) EQUAL TO QUOTES MOVE SPACE TO TEMPA(TEMPI).        
                                                                                
       FILE-WRITE.                                                              
      *                                                                         
      *    THIS CODE CREATES OR UPDATES THE ACCOUNT RECORD. ANY FIELD           
      *    WHICH HAS BEEN ENTERED IS MOVED TO THE ACCOUNT RECORD.               
      *                                                                         
           IF NAMEI   NOT = LOW-VALUE MOVE NAMEI   TO NAME   IN FILEA.          
           IF ADDRI   NOT = LOW-VALUE MOVE ADDRI   TO ADDRX  IN FILEA.          
           IF PHONEI  NOT = LOW-VALUE MOVE PHONEI  TO PHONE  IN FILEA.          
           IF DATEI   NOT = LOW-VALUE MOVE DATEI   TO DATEX  IN FILEA.          
           IF AMOUNTI NOT = LOW-VALUE MOVE AMOUNTI TO AMOUNT IN FILEA.          
           IF AMOUNTI = LOW-VALUE AND ACCOUNT-OPEN OF LINK-AREA THEN            
              MOVE INIT-AMOUNT TO AMOUNT IN FILEA.                              
           IF COMMENTI NOT = LOW-VALUE THEN                                     
              MOVE COMMENTI TO COMMENT IN FILEA.                                
                                                                                
           IF ACCOUNT-CHANGE OF LINK-AREA THEN                                  
      *                                                                         
      *       FOR AN UPDATE REQUEST THE UPDATED ACCOUNT RECORD IS               
      *       REWRITTEN TO "FILEA"                                              
      *                                                                         
             SET ACCOUNT-UPDATE OF LINK-AREA TO TRUE                            
             EXEC CICS LINK PROGRAM(CRUD-PGM-NAME)                              
                  RESP(RESPONSE) COMMAREA(LINK-AREA) END-EXEC                   
              IF RESPONSE NOT = DFHRESP(NORMAL) THEN GO TO CHECK-RESP           
              ELSE PERFORM CHECK-LINK-RESP                                      
           ELSE                                                                 
      *                                                                         
      *       FOR AN CREATE REQUEST THE NEW ACCOUNT                             
      *       RECORD IS WRITTEN TO "FILEA"                                      
             MOVE KEYNUM OF LINK-AREA TO NUMB OF FILEA                          
             SET ACCOUNT-CREATE OF LINK-AREA TO TRUE                            
             EXEC CICS LINK PROGRAM(CRUD-PGM-NAME)                              
                   RESP(RESPONSE) COMMAREA(LINK-AREA) END-EXEC                  
            IF RESPONSE NOT = DFHRESP(NORMAL) THEN GO TO CHECK-RESP             
              ELSE PERFORM CHECK-LINK-RESP.                                     
                                                                                
       DATA-ERROR.                                                              
           MOVE DFHBMASB TO MSG3A.                                              
      *                                                                         
      *    WHEN A DATA ERROR IS DETECTED THE SCREEN IS REDISPLAYED FOR          
      *    ERRORS TO BE CORRECTED. AN ERROR MESSAGE IS MOVED TO THE MAP         
      *    AREA AND HIGHLIGHTED.                                                
      *                                                                         
           MOVE 'DATA ERROR - CORRECT AND PRESS ENTER' TO MSG3O                 
      *                                                                         
      *    THE FIELD ATTRIBUTE IS SET TO MODIFIED SO DATA WILL DISPLAY          
      *    AMOUNT IS SET NUMERIC ALSO.                                          
      *                                                                         
           MOVE DFHUNNUM TO AMOUNTA.                                            
      *                                                                         
      *    THE MODIFIED DATA TAG IS SET ON FOR ALL THE DATA FIELDS SO           
      *    THAT ALL THE DATA IS RECEIVED AT THE NEXT "RECEIVE MAP".             
      *                                                                         
           MOVE DFHBMFSE TO NAMEA, ADDRA, PHONEA, DATEA,                        
                COMMENTA.                                                       
      *                                                                         
      *    THE CONTENTS OF MAP "JKMXXGB" ARE SENT TO THE SCREEN. THE            
      *    CONSTANT INFORMATION ON THE SCREEN IS NOT REFRESHED AS A             
      *    RESULT OF THE USE OF THE "DATAONLY" OPTION.                          
      *                                                                         
           EXEC CICS SEND MAP('DETAIL') MAPSET(DETAIL-MAPS)                     
                     DATAONLY END-EXEC.                                         
       CICS-CONTROL.                                                            
      *                                                                         
      *    AFTER THE "FILE ADD" OR "FILE UPDATE" SCREEN HAS BEEN                
      *    DISPLAYED THE PROGRAM BRANCHES HERE TO RETURN TO CICS                
      *    AWAITING A RESPONSE FROM THE TERMINAL. THE "RETURN" GIVES            
      *    CICS THE TRANSACTION IDENTIFIER FOR NEXT TRANSACTION AT              
      *    THIS TERMINAL TOGETHER WITH A "COMMAREA" CONTAINING ALL              
      *    INFORMATION THAT THE PROGRAM NEEDS TO CONTINUE THE UPDATE.           
      *    THE "COMMAREA" IS PASSED TO THE NEXT INVOCATION OF THIS              
      *    PROGRAM.                                                             
      *                                                                         
           EXEC CICS RETURN TRANSID(EIBTRNID) COMMAREA(LINK-AREA)               
                     END-EXEC.                                                  
      *                                                                         
      *    THIS ROUTINE GAINS CONTROL WHENEVER A CICS COMMAND RETURNS A         
      *    NON-"NORMAL" RESPONSE. THE ROUTINE EXPLICITLY CHECKS FOR THE         
      *    RESPONSES "NOTFND", "DUPREC", AND "MAPFAIL". THERE IS ALSO A         
      *    'CATCH ALL' TO TRAP ANY RESPONSE THAT IS NOT "NORMAL". THE           
      *    ROUTINE "MENU" IS CALLED TO RE-DISPLAY THE MENU MAP, ALONG           
      *    WITH AN ERROR MESSAGE.                                               
      *                                                                         
       CHECK-LINK-RESP.                                                         
      *       IF THE CURRENT FILE RECORD IS NOT THE SAME AS THE ONE             
      *       SAVED IN THE "COMMAREA" THEN ANOTHER USER HAS UPDATED THE         
      *       RECORD. A WARNING MESSAGE IS DISPLAYED, WITH FIELDS FROM          
      *       THE RECORD READ FROM "FILEA", FOR REENTRY OF THE UPDATES.         
      *                                                                         
      *       IF FILEREC IN FILEA NOT = FILEREC IN COMM-AREA THEN               
      *           RCODE = 98 IS NOW RETURNED WHEN THIS IS TRUE                  
           IF RCODE OF LINK-AREA = 98 THEN                                      
              MOVE 'RECORD UPDATED BY OTHER USER, TRY AGAIN' TO MSG1O           
              MOVE DFHBMASB TO MSG1A                                            
              MOVE DFHPROTN TO MSG3A                                            
              PERFORM MAP-BUILD                                                 
              EXEC CICS SEND MAP('DETAIL') MAPSET(DETAIL-MAPS)                  
                           END-EXEC                                             
              GO TO CICS-CONTROL.                                               
                                                                                
           IF RCODE OF LINK-AREA = DFHRESP(NOTFND) THEN                         
              MOVE 'INVALID NUMBER - PLEASE REENTER' TO MESSAGES                
              GO TO MENU.                                                       
           IF RCODE OF LINK-AREA = DFHRESP(DUPREC) THEN                         
              MOVE 'DUPLICATE RECORD' TO MESSAGES                               
              GO TO MENU.                                                       
           IF RCODE OF LINK-AREA NOT = DFHRESP(NORMAL) THEN                     
              MOVE 'TRANSACTION TERMINATED' TO MESSAGES                         
              GO TO MENU.                                                       
       CHECK-RESP.                                                              
           IF RESPONSE = DFHRESP(MAPFAIL) THEN                                  
              IF EIBCALEN = 0 THEN                                              
                 MOVE 'PRESS CLEAR TO EXIT' TO MESSAGES                         
                 GO TO MENU                                                     
              ELSE                                                              
                 MOVE 'RECORD NOT MODIFIED' TO MESSAGES                         
                 GO TO MENU.                                                    
           IF RESPONSE NOT = DFHRESP(NORMAL) THEN                               
              MOVE 'TRANSACTION TERMINATED' TO MESSAGES                         
              GO TO MENU.                                                       
      *                                                                         
      *    IF A CICS COMMAND FAILS WITH THE "ERROR" CONDITION, THE              
      *    MESSAGE "ERROR. TRANSACTION TERMINATED" IS MOVED TO                  
      *    "MESSAGES" FOR DISPLAY ON THE MENU SCREEN.                           
      *                                                                         
       ERRORS.                                                                  
           MOVE 'ERROR. TRANSACTION TERMINATED' TO MESSAGES.                    
           GO TO MENU.                                                          
       MENU.                                                                    
      *                                                                         
      *    THIS CODE GETS CONTROL WHEN AN ADD OR UPDATE IS COMPLETE.            
      *    AN INFORMATION OR ERROR MESSAGE IS IN "MESSAGES".                    
      *    THE OPERATOR INSTRUCTION MAP AREA IS CLEARED. THE MESSAGE            
      *    IS MOVED TO THE MAP AREA AND HIGHLIGHTED.                            
      *                                                                         
           MOVE LOW-VALUE TO MENUO.                                             
           MOVE DFHBMASB  TO MSGA.                                              
           MOVE MESSAGES  TO MSGO.                                              
      *                                                                         
      *    THE OPERATOR INSTRUCTION MAP "JKMXXGA" IS DISPLAYED ON AN            
      *    ERASED SCREEN.                                                       
      *                                                                         
           EXEC CICS SEND MAP('MENU') MAPSET(MENU-MAPS)                         
                     ERASE END-EXEC.                                            
      *                                                                         
      *    THE PROGRAM TERMINATES BY RETURNING TO CICS.                         
      *    NO TRANSACTION IDENTIFIER OR "COMMAREA" IS SPECIFIED.                
      *                                                                         
           EXEC CICS RETURN TRANSID(MENU-TRAN) END-EXEC.                        
           GOBACK.                                                              
