      *****************************************************************         
      * Licensed Materials - Property of IBM                          *         
      *                                                               *         
      * JKENBRVL.cbl                                                  *         
      *                                                               *         
      * B) Copyright IBM Corporation 2012                              *        
      * U.S. Government Users Restricted Rights:  Use, duplication    *         
      * or disclosure                                                 *         
      * restricted by GSA ADP Schedule Contract with IBM Corp.        *         
      *                                                               *         
      *                                                               *         
      *****************************************************************         
                                                                                
       ID DIVISION.                                                             
       PROGRAM-ID. JKENBRVL                                                     
      *                                                                         
      *    THIS PROGRAM VALIDATES THE INPUT AND                                 
      *    CONVERTS IT TO VARIOUS FORMATS.                                      
      *    IT DOES NOT CALC THE MORTGAGE.                                       
      *                                                                         
      *    (C) 2008 IBM - Jim Hildner                                           
      *        2012 IBM - MODIFED BY DAVE ELLIS.                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. FLEX-ES.                                                
       OBJECT-COMPUTER. FLEX-ES.                                                
       DATA DIVISION.                                                           
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
       01 WS-STATIC-DATA.                                                       
           03 STATIC-ERRORS.                                                    
              05 FILLER                  PIC 99 VALUE 1.                        
              05 FILLER                  PIC X(80)                              
              VALUE 'NO NUMBER PRESENT'.                                        
              05 FILLER                  PIC 99 VALUE 2.                        
              05 FILLER                  PIC X(80)                              
              VALUE 'INVALID CHARACTERS IN NUMBER'.                             
              05 FILLER                  PIC 99 VALUE 3.                        
              05 FILLER                  PIC X(80)                              
              VALUE 'TOO MANY DECIMAL POINTS'.                                  
              05 FILLER                  PIC 99 VALUE 4.                        
              05 FILLER                  PIC X(80)                              
              VALUE 'YEARS INDICATED, BUT YEARS ZERO OR LESS'.                  
              05 FILLER                  PIC 99 VALUE 5.                        
              05 FILLER                  PIC X(80)                              
              VALUE 'ZERO OR LESS MONTHS'.                                      
              05 FILLER                  PIC 99 VALUE 6.                        
              05 FILLER                  PIC X(80)                              
              VALUE ' '.                                                        
              05 FILLER                  PIC 99 VALUE 7.                        
              05 FILLER                  PIC X(80)                              
              VALUE 'LOAN AMOUNT MUST BE BETWEEN $500 AND $500,000'.            
              05 FILLER                  PIC 99 VALUE 8.                        
              05 FILLER                  PIC X(80)                              
              VALUE ' '.                                                        
              05 FILLER                  PIC 99 VALUE 9.                        
              05 FILLER                  PIC X(80)                              
              VALUE ' '.                                                        
              05 FILLER                  PIC 99 VALUE 10.                       
              05 FILLER                  PIC X(80)                              
              VALUE ' '.                                                        
           03 STATIC-ERROR-TBL REDEFINES STATIC-ERRORS.                         
              05 STATIC-ERROR-TABLE OCCURS 10 TIMES.                            
                07 ERROR-INDICATOR         PIC 99.                              
                07 ERROR-TEXT              PIC X(80).                           
       01  WS-WORK-AMOUNTS.                                                     
           03 WS-LEADING-SPACES      PIC 9(4) COMP VALUE 1.                     
           03 WS-TRAILING-SPACES     PIC 9(4) COMP VALUE 0.                     
           03 WS-END-SPACE           PIC 9(4) COMP VALUE 0.                     
           03 WS-DECIMAL-SPACE       PIC 99        VALUE 0.                     
           03 WS-IDX                 PIC 9(2) COMP.                             
           03 WS-DEC-IDX             PIC 9(2) COMP.                             
           03 WS-NUM-IDX             PIC 9(2) COMP.                             
                                                                                
           03 WS-MAX-NUMBER-LGTH     PIC 9(2) COMP.                             
           03 WS-MAX-FIELD           PIC 9(2) COMP.                             
           03 WS-DEC-ADJUST          PIC 9.                                     
                                                                                
                                                                                
       LINKAGE SECTION.                                                         
      *                                                                         
       COPY JKENBRPM.                                                           
                                                                                
       PROCEDURE DIVISION USING JKE-NUMBER-VALIDATION.                          
      *                                                                         
       A000-MAINLINE SECTION.                                                   
       A000-10.                                                                 
           MOVE JKEPARM-MAX-LENGTH              TO WS-IDX.                      
           MOVE LENGTH OF JKEPARM-VALIDATE-DATA TO WS-MAX-FIELD                 
           IF WS-IDX > WS-MAX-FIELD                                             
              MOVE WS-MAX-FIELD TO WS-IDX                                       
           ELSE                                                                 
              MOVE WS-IDX       TO WS-MAX-FIELD                                 
           END-IF.                                                              
                                                                                
           MOVE ZERO   TO WS-END-SPACE.                                         
           MOVE ZERO   TO JKEPARM-RETURN-ERROR-RC.                              
           MOVE SPACES TO JKEPARM-RETURN-ERROR-TEXT.                            
           MOVE ZERO   TO JKEPARM-BINARY-NUMBER                                 
                          JKEPARM-NUMBER                                        
                          JKEPARM-DECIMAL.                                      
                                                                                
      * FIND TRAILING SPACES                                                    
           PERFORM UNTIL WS-IDX = 0                                             
              IF JKEPARM-VALIDATE-DATA(WS-IDX:1) = SPACES                       
                ADD 1      TO WS-TRAILING-SPACES                                
                SUBTRACT 1 FROM WS-IDX                                          
              ELSE                                                              
                MOVE WS-IDX TO WS-END-SPACE                                     
                MOVE 0 TO WS-IDX                                                
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
      * FIND LEADING SPACES                                                     
           MOVE 1 TO WS-LEADING-SPACES.                                         
                                                                                
           IF WS-END-SPACE NOT = 0                                              
              MOVE 1 TO WS-IDX                                                  
              PERFORM UNTIL WS-IDX >= WS-END-SPACE                              
                IF JKEPARM-VALIDATE-DATA(WS-IDX:1) = SPACES                     
                   ADD 1 TO WS-LEADING-SPACES                                   
                   ADD 1 TO WS-IDX                                              
                ELSE                                                            
                   COMPUTE WS-IDX = WS-END-SPACE + 1                            
                END-IF                                                          
              END-PERFORM                                                       
           ELSE                                                                 
              MOVE STATIC-ERROR-TABLE(1) TO JKEPARM-RETURN-ERROR                
              GO TO A999-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE WS-LEADING-SPACES TO WS-IDX.                                    
           MOVE 1                 TO WS-DEC-IDX.                                
           MOVE 0                 TO WS-DECIMAL-SPACE.                          
                                                                                
      * FIND DECIMAL POINT                                                      
           PERFORM A002-COMPUTE-DECIMAL                                         
                   UNTIL WS-IDX > WS-END-SPACE                                  
                      OR JKEPARM-RETURN-ERROR-RC > 0                            
           .                                                                    
                                                                                
           IF  JKEPARM-RETURN-ERROR-RC > 0                                      
               GO TO A999-EXIT                                                  
           END-IF.                                                              
                                                                                
           IF WS-DECIMAL-SPACE > 0                                              
              COMPUTE WS-END-SPACE = WS-DECIMAL-SPACE - 1                       
           END-IF.                                                              
                                                                                
      * VALIDATE NO INTERNAL BLANKS                                             
           MOVE WS-END-SPACE             TO WS-IDX.                             
           MOVE LENGTH OF JKEPARM-NUMBER TO WS-NUM-IDX.                         
      *     SUBTRACT 1 FROM WS-NUM-IDX.                                         
                                                                                
           PERFORM A001-COMPUTE-INTEGER                                         
                   UNTIL WS-IDX < WS-LEADING-SPACES                             
                      OR JKEPARM-RETURN-ERROR-RC > 0                            
           .                                                                    
                                                                                
           IF  JKEPARM-RETURN-ERROR-RC > 0                                      
               GO TO A999-EXIT                                                  
           END-IF.                                                              
                                                                                
                                                                                
      *** RC Must be 0 at this point to calc value.                             
           COMPUTE JKEPARM-BINARY-NUMBER = JKEPARM-NUMBER                       
                                         + JKEPARM-DECIMAL.                     
                                                                                
      * Apply requested rule to the value:                                      
      * Loan term must be between 1 and 40 years                                
                                                                                
           IF JKEPARM-RULE-FLAG-YEARS                                           
              IF JKEPARM-BINARY-NUMBER < 1 OR                                   
                 JKEPARM-BINARY-NUMBER > 40                                     
                    MOVE STATIC-ERROR-TABLE(6) TO                               
                         JKEPARM-RETURN-ERROR                                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF JKEPARM-RULE-FLAG-AMOUNT                                          
              IF JKEPARM-BINARY-NUMBER < 500 OR                                 
                 JKEPARM-BINARY-NUMBER > 500000                                 
                    MOVE STATIC-ERROR-TABLE(7) TO                               
                         JKEPARM-RETURN-ERROR                                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       A999-EXIT.                                                               
           GOBACK.                                                              
                                                                                
       A001-COMPUTE-INTEGER SECTION.                                            
       A001-10.                                                                 
           IF JKEPARM-VALIDATE-DATA(WS-IDX:1) = ','                             
              SUBTRACT 1 FROM WS-IDX                                            
           ELSE                                                                 
              IF JKEPARM-VALIDATE-DATA(WS-IDX:1) = SPACE                        
              OR JKEPARM-VALIDATE-DATA(WS-IDX:1) IS NOT NUMERIC                 
                 MOVE STATIC-ERROR-TABLE(2) TO JKEPARM-RETURN-ERROR             
                 MOVE 0 TO WS-IDX                                               
              ELSE                                                              
                 MOVE JKEPARM-VALIDATE-DATA(WS-IDX:1) TO                        
                      JKEPARM-NUMBER(WS-NUM-IDX:1)                              
                 SUBTRACT 1 FROM WS-IDX                                         
                                 WS-NUM-IDX                                     
              END-IF                                                            
           END-IF                                                               
           .                                                                    
       A001-99.                                                                 
           EXIT.                                                                
                                                                                
       A002-COMPUTE-DECIMAL SECTION.                                            
       A002-10.                                                                 
           IF WS-DECIMAL-SPACE = 0                                              
              IF JKEPARM-VALIDATE-DATA(WS-IDX:1) = '.'                          
                 MOVE WS-IDX TO WS-DECIMAL-SPACE                                
              END-IF                                                            
           ELSE                                                                 
              IF JKEPARM-VALIDATE-DATA(WS-IDX:1) = '.'                          
                 MOVE STATIC-ERROR-TABLE(3) TO JKEPARM-RETURN-ERROR             
                 MOVE WS-END-SPACE TO WS-IDX                                    
                 MOVE 1            TO WS-DEC-IDX                                
              ELSE                                                              
                 MOVE JKEPARM-VALIDATE-DATA(WS-IDX:1) TO                        
                      JKEPARM-DECIMAL(WS-DEC-IDX:1)                             
                 ADD 1 TO WS-DEC-IDX                                            
              END-IF                                                            
           END-IF                                                               
           ADD 1 TO WS-IDX                                                      
           .                                                                    
       A002-99.                                                                 
           EXIT.                                                                
