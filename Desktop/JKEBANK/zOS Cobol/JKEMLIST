      *****************************************************************         
      * Licensed Materials - Property of IBM                          *         
      *                                                               *         
      * JKEMLIST.cbl                                                  *         
      *                                                               *         
      * B) Copyright IBM Corporation 2012                              *        
      * U.S. Government Users Restricted Rights:  Use, duplication    *         
      * or disclosure                                                 *         
      * restricted by GSA ADP Schedule Contract with IBM Corp.        *         
      *                                                               *         
      *                                                               *         
      *****************************************************************         
                                                                                
       ID DIVISION.                                                             
       PROGRAM-ID. JKEMLIST.                                                    
      *    THIS DEMONSTRATES CICS/DEBUG           - JKEDEMOS 2008               
      *                                                                         
      *    THIS PROGRAM RECEIVE A LOAN MODEL: AMOUNT, RATE, TERM.               
      *    IT THEN LOOKS FOR COMPANIES IN A VSAM FILE TO DETERMINE              
      *    ANY THAT CAN SERVICE THE LOAN AS SPECIFIED. THOSE COMPANIES          
      *    ARE DISPLAYED ON A CICS SCREEN.                                      
      *                                                                         
      *    THIS MODULE ALSO PROVIDES THE SERVICE IMPLEMENTATION FOR             
      *    THE GET COMPANIES SERVICE. IN THAT CASE, THE VALUE FOR               
      *    PROCESS_INDICATOR SENT THROUGH THE COMMAREA WILL BE 0.               
      *                                                                         
      *    (C) 2008 IBM - JIM HILDNER RESERVED.                                 
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-FLEX-ES.                                            
       OBJECT-COMPUTER. IBM-FLEX-ES.                                            
      *                                                                         
       DATA DIVISION.                                                           
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
       01  W-FLAGS.                                                             
           10  W-SEND-FLAG                    PIC X.                            
               88  SEND-ERASE                   VALUE '1'.                      
               88  SEND-DATAONLY                VALUE '2'.                      
               88  SEND-MAPONLY                 VALUE '3'.                      
               88  SEND-DATAONLY-ALARM          VALUE '4'.                      
           10 W-CICS-OUTPUT-FLAG              PIC X.                            
               88  CICS-OUTPUT                  VALUE '1'.                      
                                                                                
       01 W-CONVERSIONS.                                                        
           05  W-PMT-CNVRT     PIC X(12).                                       
           05  W-PMT-NUMBER                                                     
               REDEFINES W-PMT-CNVRT                                            
                               PIC 9(10)V99.                                    
           05  WS-FORMAT-NUMBER PIC Z,ZZZ,ZZ9.99.                               
           05  W-PRINC-CNVRT   PIC X(12).                                       
           05  W-PRINC-NUMBER                                                   
               REDEFINES W-PRINC-CNVRT                                          
                               PIC 9(10)V99.                                    
                                                                                
       01 W-CALL-PROGRAM                      PIC X(8).                         
       01 RESPONSE                            PIC S9(8) COMP.                   
       01 INTERNAL-PROGRAM-VARIABLES.                                           
           05 RID-LENGTH                      PIC S9(9) COMP.                   
           05 DISP-COUNT                      PIC S9(4) COMP.                   
           05 MAX-LOOP                        PIC S9(4) COMP                    
                                              VALUE IS 8.                       
           05 END-OF-FILE                     PIC X.                            
           05 CLOSE-FILE                      PIC X.                            
      *                                                                         
       01 W-COMMAREA-LENGTH                   PIC 9(4) COMP.                    
                                                                                
       01 SAVE-COMM-AREA.                                                       
          COPY JKEMTCOM.                                                        
                                                                                
      * Working COMMAREA to capture the list of companies                       
       01 WORKING-COMM-AREA REDEFINES SAVE-COMM-AREA.                           
           03 FILLER                   PIC X.                                   
      * Input                                                                   
           03 INPUT-LOAN-AMT            PIC S9(9)V99 COMP.                      
           03 INPUT-LOAN-YEARS          PIC S9(4)    COMP.                      
           03 INPUT-LOAN-MONTHS         PIC S9(4)    COMP.                      
           03 INPUT-LOAN-RATE           PIC S9(2)v9(3) COMP.                    
           03 INPUT-LOAN-YEAR-MONTH-IND PIC X.                                  
      * Output                                                                  
           03  COMPANY-TABLE.                                                   
              05 ROW-IDX                  PIC 9(3) COMP.                        
              05 COMPANY-REC OCCURS 8 TIMES.                                    
                 10 COMPANY-NAME               PIC X(24).                       
                 10 COMPANY-PHONE              PIC X(13).                       
                 10 COMPANY-RATE               PIC 9(3)V99.                     
                 10 COMPANY-MONTHLY-PAYMENT    PIC 9(10)V99.                    
                 10 COMPANY-LOAN-TERM          PIC 9(2).                        
           03 MONTHLY-PAYMENT              PIC S9(7)V99 COMP.                   
           03 ERRMSG            PIC X(80).                                      
           03 RETCODE           PIC 9(4).                                       
             88 REQUEST-SUCCESS VALUE 0.                                        
       01  YEAR-DIFF                        PIC 9(3) COMP.                      
       01  END-OF-TRANS-MSG                 PIC X(30)                           
             VALUE 'END OF TRANSACTION - THANK YOU'.                            
           COPY DFHAID.                                                         
      *    COPY DFHEIBLK.                                                       
                                                                                
           COPY JKEMLIS.                                                        
       01  OUTMAP REDEFINES JKEMLISO.                                           
           03 FILLER PIC X(110).                                                
           03 OUTMAP-REPEATE OCCURS 8 TIMES.                                    
              05 FILLER                    PIC X(5).                            
              05 OUTMAP-COMPANY            PIC X(24).                           
              05 FILLER                    PIC X(5).                            
              05 OUTMAP-PHONE-NUM          PIC X(13).                           
              05 FILLER                    PIC X(5).                            
              05 OUTMAP-RATE               PIC X(5).                            
              05 FILLER                    PIC X(5).                            
              05 OUTMAP-LOAN               PIC X(12).                           
              05 FILLER                    PIC X(5).                            
              05 OUTMAP-YEARS              PIC X(2).                            
           03 FILLER                       PIC X(5).                            
           03 OUTMAP-MSG                   PIC X(40).                           
                                                                                
           COPY JKENBRPM.                                                       
                                                                                
           COPY JKEMORTF.                                                       
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
       01 DFHCOMMAREA.                                                          
       COPY JKEMTCOM.                                                           
                                                                                
       PROCEDURE DIVISION USING DFHCOMMAREA.                                    
                                                                                
      * JPH - DO I need this for files                                          
      * EXEC CICS HANDLE CONDITION H900-NOT-FOUND                               
                                                                                
                                                                                
       JKEMLIST-MAINLINE.                                                       
           INITIALIZE SAVE-COMM-AREA.                                           
           MOVE LOW-VALUES TO SAVE-COMM-AREA.                                   
           INITIALIZE MORTGAGE-COMPANY-INFO.                                    
           MOVE LOW-VALUES TO MORTGAGE-COMPANY-INFO.                            
           INITIALIZE OUTMAP.                                                   
           MOVE LOW-VALUES TO OUTMAP.                                           
                                                                                
           MOVE LENGTH OF DFHCOMMAREA to W-COMMAREA-LENGTH.                     
           MOVE DFHCOMMAREA           TO SAVE-COMM-AREA.                        
                                                                                
      * If this module is invoked through the service interface                 
      * indicate that no CICS output processing should occur                    
           IF PROCESS-INDICATOR OF SAVE-COMM-AREA = '9'                         
              SET CICS-OUTPUT TO TRUE.                                          
                                                                                
           EVALUATE TRUE                                                        
               WHEN EIBCALEN = ZERO                                             
      * First time in - Show Screen                                             
                   PERFORM A100-PROCESS-MAP                                     
               WHEN EIBAID = DFHCLEAR                                           
      * Process CLEAR key                                                       
                   EXEC CICS RETURN END-EXEC                                    
               WHEN EIBAID = DFHPF3 OR DFHPF12                                  
      * Process END/RETURN keys                                                 
                   EXEC CICS RETURN END-EXEC                                    
               WHEN EIBAID = DFHENTER                                           
      * Process ENTER Key                                                       
                   PERFORM A100-PROCESS-MAP                                     
               WHEN OTHER                                                       
      * Present Invalid Key                                                     
                   PERFORM A100-PROCESS-MAP                                     
           END-EVALUATE                                                         
           .                                                                    
           MOVE SAVE-COMM-AREA TO DFHCOMMAREA.                                  
           EXEC CICS RETURN END-EXEC.                                           
                                                                                
       A100-PROCESS-MAP.                                                        
                                                                                
           IF CICS-OUTPUT                                                       
               PERFORM A310-ERASE-MAP.                                          
                                                                                
           MOVE 0      TO RID-LENGTH.                                           
           MOVE 'N'    TO CLOSE-FILE.                                           
           MOVE 'N'    TO END-OF-FILE.                                          
           MOVE 0      TO ROW-IDX.                                              
           MOVE LOW-VALUES TO COMPANY-TABLE.                                    
                                                                                
           EXEC CICS STARTBR DATASET('JKEMORTF')                                
                     RIDFLD(RID-LENGTH) RBA                                     
                     EQUAL                                                      
                     RESP(RESPONSE) END-EXEC.                                   
                                                                                
           IF (RESPONSE = DFHRESP(NORMAL))                                      
              MOVE 'Y' TO CLOSE-FILE                                            
              MOVE 1   TO DISP-COUNT                                            
              PERFORM A150-PROCESS-FILE                                         
                      UNTIL END-OF-FILE = 'Y'                                   
                      OR    DISP-COUNT  > MAX-LOOP                              
           ELSE                                                                 
              MOVE 'ERROR WITH START'         TO EPCMP1O                        
              MOVE RESPONSE                   TO EPLOAN1O                       
           END-IF                                                               
           .                                                                    
                                                                                
                                                                                
           IF CLOSE-FILE = 'Y'                                                  
            EXEC CICS ENDBR FILE('JKEMORTF') END-EXEC                           
           END-IF                                                               
           .                                                                    
           IF CICS-OUTPUT                                                       
               PERFORM A300-SEND-MAP.                                           
                                                                                
       A150-PROCESS-FILE.                                                       
                                                                                
           EXEC CICS READNEXT FILE('JKEMORTF')                                  
                    INTO(MORTGAGE-COMPANY-INFO)                                 
                    RIDFLD(RID-LENGTH)                                          
                    RBA RESP(RESPONSE)                                          
           END-EXEC                                                             
           .                                                                    
                                                                                
           IF (RESPONSE = DFHRESP(NORMAL))                                      
               IF  INPUT-LOAN-AMT <= MORT-FILE-LOAN                             
                    AND INPUT-LOAN-RATE >= MORT-FILE-RATE                       
                    AND INPUT-LOAN-YEARS = MORT-FILE-YEARS                      
      * If the loan amount specified by the user is LESS THAN the               
      * loan amount the company offers AND                                      
      * the loan rate specified by the user is GREATER THAN the loan            
      * rate the company offers AND                                             
      * the loan term is equal to what the company offers,                      
      * add the company to the list.                                            
      *                                                                         
      * Move LOAN COMPANY NAME to output map                                    
                 MOVE MORT-FILE-COMPANY TO                                      
                       OUTMAP-COMPANY(DISP-COUNT)                               
      * Move LOAN COMPANY PHONE NUMBER to output map                            
                 MOVE MORT-FILE-PHONE-NUM TO                                    
                       OUTMAP-PHONE-NUM(DISP-COUNT)                             
      * Move LOAN COMPANY RATE offered to output map                            
                 MOVE MORT-FILE-RATE TO WS-FORMAT-NUMBER                        
                 MOVE WS-FORMAT-NUMBER(7:5)                                     
                                       TO OUTMAP-RATE(DISP-COUNT)               
      * Move LOAN COMPANY LOAN TERM to output map                               
                 MOVE MORT-FILE-YEARS TO                                        
                       OUTMAP-YEARS(DISP-COUNT)                                 
      * Calculate the mortgage for the loan modeled for this company            
                 PERFORM A600-CALCULATE-MORTGAGE                                
                 MOVE JKEPCOM-RETURN-MONTH-PAYMENT OF DFHCOMMAREA               
                                       TO WS-FORMAT-NUMBER                      
                 MOVE WS-FORMAT-NUMBER TO OUTMAP-LOAN(DISP-COUNT)               
                 MOVE WS-FORMAT-NUMBER TO MORT-FILE-LOAN                        
      * Increase read count to get next line in file                            
                 ADD 1 TO DISP-COUNT                                            
                                                                                
      * Add company to list to return                                           
      ** Need to add loan amount, maybe other things **                         
                 ADD 1 TO ROW-IDX                                               
                 MOVE MORTGAGE-COMPANY-INFO                                     
                             TO COMPANY-REC(ROW-IDX)                            
                                                                                
              END-IF                                                            
           ELSE                                                                 
              IF (RESPONSE NOT = DFHRESP(ENDFILE))                              
                 MOVE 'ERROR WITH READ NEXT' TO EPCMP1O                         
                 MOVE RESPONSE               TO EPLOAN1O                        
              ELSE                                                              
                 MOVE 'Y' TO END-OF-FILE                                        
              END-IF                                                            
           END-IF                                                               
           .                                                                    
                                                                                
       A300-SEND-MAP.                                                           
           EVALUATE TRUE                                                        
              WHEN SEND-MAPONLY                                                 
                   EXEC CICS                                                    
                     SEND MAP ('JKEMLIS')                                       
                       MAPSET('JKEMLIS')                                        
                       MAPONLY                                                  
                       FREEKB                                                   
                       CURSOR                                                   
                   END-EXEC                                                     
              WHEN SEND-ERASE                                                   
                   EXEC CICS                                                    
                     SEND MAP ('JKEMLIS')                                       
                         MAPSET('JKEMLIS')                                      
                         FROM(JKEMLISO)                                         
                         ERASE                                                  
                         FREEKB                                                 
                         CURSOR                                                 
                   END-EXEC                                                     
              WHEN SEND-DATAONLY                                                
                   EXEC CICS                                                    
                     SEND MAP ('JKEMLIS')                                       
                         MAPSET('JKEMLIS')                                      
                         FROM(JKEMLISO)                                         
                         DATAONLY                                               
                         CURSOR                                                 
                         FREEKB                                                 
                   END-EXEC                                                     
              WHEN OTHER                                                        
                   EXEC CICS                                                    
                     SEND MAP ('JKEMLIS')                                       
                         MAPSET('JKEMLIS')                                      
                         FROM(JKEMLISO)                                         
                         FREEKB                                                 
                     END-EXEC.                                                  
       A300-EXIT.                                                               
           EXIT.                                                                
                                                                                
       A310-ERASE-MAP.                                                          
            MOVE LOW-VALUES TO JKEMLISO.                                        
            SET SEND-ERASE TO TRUE                                              
            PERFORM A300-SEND-MAP.                                              
                                                                                
       A600-CALCULATE-MORTGAGE.                                                 
      * Calculate the mortgage offered by the company listed                    
      * based on the loan rate and term offered by that company                 
      * using the amount specified by the user.                                 
           MOVE 'Y' TO JKEPCOM-YEAR-MONTH-IND                                   
                                 OF DFHCOMMAREA.                                
           MOVE MORT-FILE-RATE   TO JKEPCOM-QUOTED-INTEREST-RATE                
                                 OF DFHCOMMAREA.                                
           MOVE 'JKECSMRT' TO W-CALL-PROGRAM                                    
           EXEC CICS LINK PROGRAM( W-CALL-PROGRAM )                             
                          COMMAREA( DFHCOMMAREA )                               
           END-EXEC.                                                            
                                                                                
      * USED FOR TESTING                                                        
       A300-TRY2.                                                               
           DISPLAY 'Read Company Interest Rate: ' MORT-FILE-RATE.               
           DISPLAY 'Read Company Loan Term: ' MORT-FILE-YEARS.                  
           DISPLAY 'Read Company Name: ' MORT-FILE-COMPANY.                     
